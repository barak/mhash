dnl Process this file with autoconf to produce a configure script.
AC_INIT(mhash.c)
AM_INIT_AUTOMAKE(mhash, 0.6.1)

MHASH_VERSION=$VERSION
AC_SUBST(MHASH_VERSION)

AM_CONFIG_HEADER(mhash_config.h)

AM_MAINTAINER_MODE
	
AC_PROG_MAKE_SET

AC_ARG_WITH(CC,
[  --with-CC               choose compiler],
  if test "$withval" != "no" && test "$withval" != "yes" ; then
    CC="$withval"
  fi
)

AC_ARG_WITH(CFLAGS,
[  --with-CFLAGS           set C compiler flags],
  if test "$withval" != "no" && test "$withval" != "yes" ; then
    CFLAGS="$withval"
  fi
)

AC_ARG_WITH(CPPFLAGS,
[  --with-CPPFLAGS         set C preprocessor flags],
  if test "$withval" != "no" && test "$withval" != "yes" ; then
    CPPFLAGS="$withval"
  fi
)

AC_ARG_WITH(LDFLAGS,
[  --with-LDFLAGS          set linker flags],
  if test "$withval" != "no" && test "$withval" != "yes" ; then
    LDFLAGS="$withval"
  fi
)

AC_PROG_CC

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_LN_S

dnl if test $ac_cv_prog_gcc = yes; then
dnl  CFLAGS="${CFLAGS} --fast-math -Wall -Wpointer-arith -pedantic"
dnl fi

AC_HEADER_STDC
AC_CHECK_HEADERS(sys/stat.h sys/types.h fcntl.h signal.h unistd.h utime.h \
pthread.h)
AC_CHECK_FUNCS(bzero signal sigaction memset fcntl fsync \
lstat stat umask utime memmove bcopy)

dnl Checks for libraries.
AC_CHECK_SIZEOF(unsigned long long int, 8)
AC_CHECK_SIZEOF(unsigned long int, 4)
AC_CHECK_SIZEOF(unsigned int, 4)
AC_CHECK_SIZEOF(unsigned short int, 2)
AC_CHECK_SIZEOF(unsigned char, 1)

AC_C_BIGENDIAN

AM_PROG_LIBTOOL
AM_SET_LIBTOOL_VARIABLE([--silent])

AC_MSG_CHECKING([whether posix threads are requested])
AC_ARG_ENABLE(pthreads,
[  --disable-pthreads      disable the use of POSIX threads],
    try_posix_threads=$enableval, try_posix_threads=yes)
AC_MSG_RESULT($try_posix_threads)

AC_CHECK_LIB(c_r, pthread_kill)
AC_CHECK_LIB(pthread, pthread_kill)

AC_CHECK_FUNCS(pthread_kill)

if test "$try_posix_threads" = "yes"; then
  if test "$ac_cv_func_pthread_kill" != "yes"; then
    AC_MSG_ERROR(You need pthreads for proper thread handling. Alternatively, configure with --disable-pthreads)
  fi
  CFLAGS="-D_REENTRANT $CFLAGS"
  AC_DEFINE(MHASH_PTHREADS)
fi

AC_OUTPUT([Makefile doc/Makefile])
